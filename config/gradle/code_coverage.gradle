static String resolveProjectVariantForCodeCoverage(Project project) {
    if (project.name == "WordPress") {
        return "wordpressVanillaRelease"
    } else {
        return "release"
    }
}

allprojects {
    pluginManager.withPlugin("org.jetbrains.kotlinx.kover") {
        kover {
            if (project.plugins.hasPlugin("com.android.library") || project.plugins.hasPlugin("com.android.application")) {
                currentProject {
                    createVariant("custom") {
                        addWithDependencies([resolveProjectVariantForCodeCoverage(project)] as String[], false)
                    }
                }
            }

            reports {
                filters {
                    excludes {
                        classes(
                                // Package exclusions - converted to class patterns
                                'com.bumptech.glide.*',
                                'dagger.*',
                                '*.compose*',
                                '*.debug*',
                                'hilt_aggregated_deps.*',
                                '*.databinding.*',
                                'org.wordpress.android.modules.*',
                                'org.wordpress.android.widgets.*',
                                // Class pattern exclusions
                                '*_Factory*',
                                '*Activity',
                                '*Activity$*',
                                '*Adapter',
                                '*Adapter$*',
                                '*BuildConfig',
                                '*DiffCallback*',
                                '*Dialog',
                                '*Dialog$*',
                                '*Fragment',
                                '*Fragment$*',
                                '*FragmentDirections*',
                                '*FragmentKt*',
                                '*Module',
                                '*Module_*',
                                '*View',
                                '*View$*',
                                '*ViewHolder',
                                '*ViewHolder$*',
                                '*ViewHolderKt*',
                                '*.Hilt_*',
                                '*HiltModules*',
                                '*_MembersInjector',
                        )
                    }
                }
            }
        }
    }
}

dependencies {
    kover(
            project(":WordPress"),
            project(":libs:editor"),
            project(":libs:image-editor"),
            project(":libs:processors"),
            project(":libs:fluxc"),
            project(":libs:login")
    )
}

